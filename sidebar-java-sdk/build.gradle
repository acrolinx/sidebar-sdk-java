def buildNumber = System.getenv('TRAVIS_BUILD_NUMBER');

def buildVersion = buildNumber != null ? buildNumber : 123

def artifactName = "sidebar-sdk"
def fullVersion = "${project.version}.${buildVersion}"

def isReleaseBuild = !fullVersion.contains('SNAPSHOT')

def isMasterBranch = false

task getIsMasterBranch {
	def branch = ""
	def proc = "git rev-parse --abbrev-ref HEAD".execute()
	proc.in.eachLine { line -> branch = line }
	proc.err.eachLine { line -> println line }
	proc.waitFor()
	if(branch.startsWith('master')) {
		isMasterBranch=true
	} else {
		isMasterBranch=false
	}
}

group = 'com.acrolinx.client'


task createVersionPropertiesFile {
	doFirst {
		def Properties versionProps = new Properties()
		def versionPropsFile = file('versionJavaSDK.properties')
		if (versionPropsFile.exists())
			versionProps.load(new FileInputStream(versionPropsFile))
		def code = "${fullVersion}"
		versionProps['VERSION_JAVA_SDK'] = code.toString()
		versionProps.store(versionPropsFile.newWriter(), null)
	}
}

jar{
	archiveName = "${artifactName}-${fullVersion}.jar"
	manifest {
		attributes(
				"Implementation-Title": "${artifactName}",
				"Implementation-Version": "${fullVersion}",
				"Specification-Title": "${artifactName}",
				"Specification-Version": project.version
		)

	}
	from("$projectDir") {
		include 'versionJavaSDK.properties'
	}
	from("$rootDir") {
		include 'dependency-licenses/*.html'
	}
}

task generateThirdPartyLibraryReport(type: Copy, dependsOn: dependencyLicenseReport) {
	from('build/reports/dependency-license') {
		include '*.html'
	}
	into('../dependency-licenses')
}

task copyServerSelectorRessources(type: Copy, dependsOn: npm_install) {

	doFirst {
		def mainDir = new File('src/main/resources')
		def subDir = new File(mainDir, 'server-selector')
		if (subDir.exists()) {
			def result = subDir.deleteDir()
			assert result
		}
		subDir.mkdirs()
		assert subDir.exists()
	}

	from('node_modules/acrolinx-sidebar-startpage/dist/dist-offline')
	into('src/main/resources/server-selector')
}

task convertLocalizationEncoding() {
	def dir = new File("$projectDir/src/main/resources/localization_iso-8859-1/")
	if (!dir.exists()) {
		dir.mkdirs()
	}
	def sourceDir = new File("$projectDir/src/main/resources/localization")
	sourceDir.eachFile {
		pFile ->
			if (pFile.name.endsWith(".properties")) {
				def f = pFile.getText('utf-8')
				def pFileName = pFile.name
				if (!pFileName.contains("_ja")){
					def write = new File("$projectDir/src/main/resources/localization_iso-8859-1/" + pFileName)
					write.createNewFile();
					write.write(f, 'ISO-8859-1')
				} else {
					def write = new File("$projectDir/src/main/resources/localization_iso-8859-1/" + pFileName)
					write.createNewFile();
					StringBuilder b = new StringBuilder();
					for (char c : f.toCharArray()) {
						//noinspection GroovyAssignabilityCheck
						if (c >= 128)
						//noinspection GroovyAssignabilityCheck
							b.append("\\u").append(String.format("%04X", (int) c));
						else
							b.append(c);
					}
					def string = b.toString();
					write.write(string, 'ISO-8859-1')
				}

			}
	}
}

task generateJavaDocs(type: Javadoc) {
	source = sourceSets.main.allJava
	destinationDir = new File(rootProject.projectDir, "docs/")
	failOnError = false
}

task javadocJar(type: Jar, dependsOn: 'generateJavaDocs') {
	baseName = "${artifactName}"
	version = "${fullVersion}"
	classifier = 'javadoc'
	from "${rootProject.projectDir}/docs"
}

task sourceJar(type: Jar) {
	baseName = "${artifactName}"
	version = "${fullVersion}"
	classifier = 'sources'
	from project.sourceSets.main.allSource
}

ext {
	pomFilePath = "${project.projectDir}/build/repo/com/acrolinx/client/sidebar-sdk/${version}/sidebar-sdk-${version}.pom"
	pomFile = file(pomFilePath)
}
// add configuration for pom signing
configurations {
	pom
}

artifacts {
	archives jar
	archives javadocJar
	archives sourceJar
	if (project.ext.pomFile.exists()) {
		pom pomFile
	}
}

// sign all artifacts
task signJars (type : Sign, dependsOn: [jar, javadocJar, sourceJar]) {
	sign configurations.archives
}
// sign pom
task signPom(type: Sign) {
	sign configurations.pom
}

// defining which tasks should be called
if (project.ext.pomFile.exists() && isReleaseBuild) {
	task preparePublication (dependsOn : [signJars, signPom])
} else if (isReleaseBuild) {
	task preparePublication (dependsOn : signJars)
}

// extract signatures and add classifier and extension to them
def getSignatureFiles = {
	def allFiles = project.tasks.signJars.signatureFiles.collect { it }
	def signedSources = allFiles.find { it.name.contains('-sources') }
	def signedJavadoc = allFiles.find { it.name.contains('-javadoc') }
	def signedJar = (allFiles - [signedSources, signedJavadoc])[0]
	return [
			[archive: signedSources, classifier: 'sources', extension: 'jar.asc'],
			[archive: signedJavadoc, classifier: 'javadoc', extension: 'jar.asc'],
			[archive: signedJar,     classifier: null,      extension: 'jar.asc']
	]
}
// extract pom signature
def getPomSignature = {
	return project.tasks.signPom.signatureFiles.collect{it}[0]
}

def devId = "Acrolinx Open Source"
def devName = "Franziska PreiÃŸ"
def devEmail = "opensource@acrolinx.com"


publishing {
	publications {
		if (isReleaseBuild){
			gpgJars(MavenPublication) {
				getSignatureFiles().each {signature ->
					artifact (signature.archive) {
						classifier = signature.classifier
						extension  = signature.extension
					}
				}
			}
		}
		if (project.ext.pomFile.exists() && isReleaseBuild) {
			gpgPom(MavenPublication) {
				artifact (getPomSignature()) {
					classifier = null
					extension  = 'pom.asc'
				}
			}
		}
		jar(MavenPublication) {
			from components.java
			artifact sourceJar
			artifact javadocJar
			pom.withXml {
				Node pomNode = asNode()
				pomNode.dependencies.'*'.findAll() {
					it.groupId.text() == 'eclipse-swt-deps'
				}.each() {
					it.parent().remove(it)
				}
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'sidebar-sdk'
					description 'Acrolinx Sidebar SDK to build Acrolinx integrations for Java FX, Swing or SWT based clients.'
					url 'https://github.com/acrolinx/sidebar-sdk-java'
					scm {
						url 'https://github.com/acrolinx/sidebar-sdk-java'
						connection 'scm:git@github.com:acrolinx/sidebar-sdk-java.git'
						developerConnection 'scm:git@github.com:acrolinx/sidebar-sdk-java.git'
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					if (isReleaseBuild){
					developers {
							developer {
								id devId
								name devName
								email devEmail
								organization 'Acrolinx'
								organizationUrl 'https://www.acrolinx.com/'
							}
						}
					}
				}
			}
		}
	}
	repositories {
		maven {
		if (isReleaseBuild && project.ext.pomFile.exists()) {
			url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = sonatypeUsername
				password = sonatypePassword
			}
		}
		else if(sonatypeUsername == null || (isReleaseBuild && !project.ext.pomFile.exists())){
				url "$buildDir/repo"
		}
		else if(!isReleaseBuild) {
				url "https://oss.sonatype.org/content/repositories/snapshots/"
				credentials {
					username = sonatypeUsername
					password = sonatypePassword
				}
			}
		}
	}
}

task publishDocsAndCreateGithubReleaseTag {
	doLast{
		println 'publishDocsAndCreateGithubReleaseTag'
		if(isReleaseBuild){
			println 'starting push to github task'
		def hasOldReleaseVersion = grgit.tag.list().find { it.getName() == "release-${project.version}" }
		if (!hasOldReleaseVersion) {
			println 'is new release version'
			grgit.add(patterns: ['docs', 'dependency-licenses'])
			grgit.commit(message: "Release version ${project.version}")
			grgit.push(force: true)
			grgit.tag.add(name: "release-${project.version}", message: "Releasing ${project.version}")
			grgit.push(tags: true)
		}
		}
	}

}

//npm_install.dependsOn npm_cache_clean

//noinspection GroovyAssignabilityCheck,GroovyAssignabilityCheck
processResources.dependsOn copyServerSelectorRessources
processResources.dependsOn convertLocalizationEncoding
processResources.dependsOn  generateThirdPartyLibraryReport
jar.dependsOn createVersionPropertiesFile
