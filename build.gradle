import groovy.transform.Field

import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption
import java.util.stream.Collectors
import java.util.zip.ZipEntry
import java.util.zip.ZipFile

plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'com.github.node-gradle.node' version '7.1.0'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.sonarqube' version '7.2.2.6593'
    id 'com.diffplug.spotless' version '8.2.1'
}

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {
        substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') using module("org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION")
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.13.2'
    api 'org.apache.commons:commons-text:1.15.0'
    api 'org.jsoup:jsoup:1.22.1'
    api 'org.slf4j:slf4j-api:1.7.36'

    compileOnly "org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION"

    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.36'
}

group = 'com.acrolinx.client'
version = currentVersion

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': rootProject.name,
                'Implementation-Version': version,
                'Specification-Title': rootProject.name,
                'Specification-Version': version
        )
    }
    from(project.projectDir) {
        include 'LICENSE'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

javafx {
    version = '18'
    modules = ['javafx.swing', 'javafx.web']
}

nexusPublishing {
    repositories {
        // see https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

processResources {
    dependsOn 'zipSidebarStartpage'

    doLast {
        File resourcesDir = sourceSets.main.output.resourcesDir
        resourcesDir.mkdirs()
        File versionPropertyFile = new File(resourcesDir, 'sidebar-sdk-java-version.properties')
        versionPropertyFile.text = "sidebar-sdk-java-version=$currentVersion"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set('sidebar-sdk-java')
                description.set('Acrolinx Sidebar SDK to build Acrolinx integrations for Java FX, Swing or SWT based clients.')
                url.set('https://github.com/acrolinx/sidebar-sdk-java')
                licenses {
                    license {
                        name.set('The Apache Software License, Version 2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }
                developers {
                    developer {
                        id.set('Acrolinx Open Source')
                        name.set('Acrolinx')
                        email.set('opensource@acrolinx.com')
                    }
                }
                scm {
                    connection.set('scm:git@github.com:acrolinx/sidebar-sdk-java.git')
                    developerConnection.set('scm:git@github.com:acrolinx/sidebar-sdk-java.git')
                    url.set('https://github.com/acrolinx/sidebar-sdk-java')
                }
            }
        }
    }
}

signing {
    required = false
    final String signingKey = findProperty('signingKey')
    final String signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

sonar {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'acrolinx'
        property 'sonar.projectKey', 'acrolinx_sidebar-sdk-java'
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR Acrolinx GmbH */'
    }
    format 'misc', {
        target '.gitignore', '*.gradle', '*.md', '*.properties'

        leadingTabsToSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.register('incrementSnapshotVersion') {
    doLast {
        println("Current version: $currentVersion")
        final String newSnapshotVersion = createNewSnapshotVersion(currentVersion)

        println("New snapshot version: $newSnapshotVersion")

        updateVersionProperty(newSnapshotVersion)

        setGitHubOutput('NEW_SNAPSHOT_VERSION', newSnapshotVersion)
    }
}

tasks.register('removeSnapshotSuffix') {
    doLast {
        println("Current version: $currentVersion")

        if (!currentVersion.endsWith(SNAPSHOT_SUFFIX)) {
            throw new IllegalStateException('Current version is not a snapshot: ' + currentVersion)
        }

        final String releaseVersion = currentVersion.replace(SNAPSHOT_SUFFIX, '')

        println("Stable version: $releaseVersion")

        updateVersionProperty(releaseVersion)

        setGitHubOutput('OLD_SNAPSHOT_VERSION', "$currentVersion")
        setGitHubOutput('RELEASE_VERSION', "$releaseVersion")
    }
}

tasks.register('zipSidebarStartpage', Zip) {
    dependsOn npmInstall

    from('node_modules/@acrolinx/sidebar-startpage/dist/dist-offline/')
    include '*/**'

    archiveFileName = 'sidebar-startpage.zip'
    destinationDirectory = file("${rootProject.layout.buildDirectory.get()}/resources/main")

    doLast {
        RegularFile regularFile = layout.buildDirectory.file('resources/main/sidebar-startpage.zip').get()

        try (ZipFile zipFile = new ZipFile(regularFile.getAsFile())) {
            if (zipFile.getEntry('index.html') == null) {
                throw new GradleException('Zip file does not contain an index.html file')
            }
        }
    }
}

@Field
private static final String OSGI_PLATFORM = getOsgiPlatformValue()
@Field
private static final String SNAPSHOT_SUFFIX = '-SNAPSHOT'
@Field
private static final String SWT_VERSION = '3.123.0'

private static String createNewSnapshotVersion(final String versionString) {
    final String[] versionParts = versionString.split('\\.')
    final int incrementedPatchVersion = Integer.parseInt(versionParts[versionParts.length - 1]) + 1

    return versionParts[0] + '.' + versionParts[1] + '.' + incrementedPatchVersion + SNAPSHOT_SUFFIX
}

private static String getOsgiPlatformValue() {
    String osName = System.getProperty('os.name').toLowerCase()
    String osArch = System.getProperty('os.arch')

    if (osName.contains('windows')) {
        return 'win32.win32.x86_64'
    } else if (osName.contains('linux')) {
        return 'gtk.linux.x86_64'
    } else if (osName.contains('mac')) {
        return 'cocoa.macosx.' + osArch
    }

    throw new GradleException('unknown OS name: ' + osName)
}

private static void setGitHubOutput(final String name, final String value) {
    final String gitHubOutput = System.getenv('GITHUB_OUTPUT')
    final java.nio.file.Path gitHubOutputPath = Paths.get(gitHubOutput)
    Files.write(gitHubOutputPath, "$name=$value\n".getBytes(StandardCharsets.UTF_8), StandardOpenOption.APPEND)
}

private static void updateVersionProperty(final String versionString) {
    final String propertyName = 'currentVersion'

    final java.nio.file.Path gradlePropertiesPath = Paths.get('gradle.properties')

    if (!Files.exists(gradlePropertiesPath)) {
        throw new IllegalStateException('File does not exist: ' + gradlePropertiesPath)
    }

    final List<String> propertyFileContent = Files.readAllLines(gradlePropertiesPath)

    final List<String> updatedPropertyFileContent = propertyFileContent.stream()
            .map(line -> {
                if (line.startsWith("$propertyName=")) {
                    "$propertyName=$versionString"
                } else {
                    line
                }
            })
            .collect(Collectors.toList())

    Files.write(gradlePropertiesPath, updatedPropertyFileContent)
}
