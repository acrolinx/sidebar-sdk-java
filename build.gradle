import groovy.transform.Field

import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.nio.file.Paths
import java.nio.file.StandardOpenOption
import java.util.stream.Collectors

plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'com.github.node-gradle.node' version '7.0.2'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'com.diffplug.spotless' version '6.25.0'
}

private static String getOsgiPlatformValue() {
    String osName = System.getProperty('os.name').toLowerCase()

    if (osName.contains('windows')) {
        return 'win32.win32.x86_64'
    } else if (osName.contains('linux')) {
        return 'gtk.linux.x86_64'
    } else if (osName.contains('mac')) {
        return 'cocoa.macosx.x86_64'
    }

    throw new GradleException('unknown OS name: ' + osName)
}

@Field
private static final String SWT_VERSION = '3.123.0'
@Field
private static final String OSGI_PLATFORM = getOsgiPlatformValue()
@Field
private static final String SNAPSHOT_SUFFIX = "-SNAPSHOT"

dependencies {
    api 'com.google.code.gson:gson:2.10.1'
    api 'org.apache.commons:commons-text:1.11.0'
    api 'ch.qos.logback:logback-classic:1.5.0'
    api 'org.slf4j:slf4j-api:2.0.12'
    api 'org.jsoup:jsoup:1.17.2'
    compileOnly "org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION"

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

repositories {
    mavenCentral()
}

node {
    download = true
}

sonar {
    properties {
        property "sonar.organization", "acrolinx"
        property "sonar.projectKey", "acrolinx_sidebar-sdk-java"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

private String createNewSnapshotVersion() {
    final String[] currentVersion = currentVersion.split("\\.")
    final int incrementedPatchVersion = Integer.parseInt(currentVersion[currentVersion.length - 1]) + 1

    return currentVersion[0] + '.' + currentVersion[1] + '.' + incrementedPatchVersion + SNAPSHOT_SUFFIX
}

private static void updateVersionProperty(final String versionString) {
    final String propertyName = 'currentVersion'

    final java.nio.file.Path gradlePropertiesPath = Paths.get("gradle.properties")

    if (!Files.exists(gradlePropertiesPath)) {
        throw new IllegalStateException("File does not exist: " + gradlePropertiesPath)
    }

    final List<String> propertyFileContent = Files.readAllLines(gradlePropertiesPath)

    final List<String> updatedPropertyFileContent = propertyFileContent.stream()
            .map(line -> {
                if (line.startsWith("$propertyName=")) {
                    "$propertyName=$versionString"
                } else {
                    line
                }
            })
            .collect(Collectors.toList())

    Files.write(gradlePropertiesPath, updatedPropertyFileContent)
}

private static void setGithubOutput(final String name, final String value) {
    final String githubOutput = System.getenv('GITHUB_OUTPUT')
    final java.nio.file.Path githubOutputPath = Paths.get(githubOutput)
    Files.write(githubOutputPath, "$name=$value\n".getBytes(StandardCharsets.UTF_8), StandardOpenOption.APPEND)
}

tasks.register('removeSnapshotSuffix') {
    doLast {
        println("Current version: $currentVersion")

        if (!currentVersion.endsWith(SNAPSHOT_SUFFIX)) {
            throw new IllegalStateException("Current version is not a snapshot: " + currentVersion)
        }

        final String releaseVersion = currentVersion.replace(SNAPSHOT_SUFFIX, "")

        println("Stable version: $releaseVersion")

        updateVersionProperty(releaseVersion)

        setGithubOutput("OLD_SNAPSHOT_VERSION", "$currentVersion")
        setGithubOutput("RELEASE_VERSION", "$releaseVersion")
    }
}

tasks.register('incrementSnapshotVersion') {
    doLast {
        println("Current version: $currentVersion")
        final String newSnapshotVersion = createNewSnapshotVersion()

        println("New snapshot version: $newSnapshotVersion")

        updateVersionProperty(newSnapshotVersion)

        setGithubOutput("NEW_SNAPSHOT_VERSION", newSnapshotVersion)
    }
}

group = 'com.acrolinx.client'
version = currentVersion

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes(
                "Implementation-Title": rootProject.name,
                "Implementation-Version": version,
                "Specification-Title": rootProject.name,
                "Specification-Version": version
        )
    }
    from(project.projectDir) {
        include 'LICENSE'
    }
}

processResources {
    dependsOn npm_install

    from('node_modules/@acrolinx/sidebar-startpage/dist/dist-offline/') {
        into 'server-selector'
    }
}

javadoc {
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                name.set("sidebar-sdk-java")
                description.set("Acrolinx Sidebar SDK to build Acrolinx integrations for Java FX, Swing or SWT based clients.")
                url.set("https://github.com/acrolinx/sidebar-sdk-java")
                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("Acrolinx Open Source")
                        name.set("Acrolinx")
                        email.set("opensource@acrolinx.com")
                    }
                }
                scm {
                    connection.set("scm:git@github.com:acrolinx/sidebar-sdk-java.git")
                    developerConnection.set("scm:git@github.com:acrolinx/sidebar-sdk-java.git")
                    url.set("https://github.com/acrolinx/sidebar-sdk-java")
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

signing {
    required = false
    final String signingKey = findProperty("signingKey")
    final String signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

javafx {
    modules = ['javafx.swing', 'javafx.web']
}

configurations.all {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('org.eclipse.platform:org.eclipse.swt.${osgi.platform}') using module("org.eclipse.platform:org.eclipse.swt.$OSGI_PLATFORM:$SWT_VERSION")
        }
    }
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        licenseHeader '/* Copyright (c) $YEAR Acrolinx GmbH */'
    }
    format 'misc', {
        target '.gitignore', '*.gradle', '*.md', '*.properties'

        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}
